group = 'it.ohalee.basementlib'
project.version = '1.2'

jar {
    manifest {
        attributes('Automatic-Module-Name': 'it.ohalee.basementlib.api')
    }
}

dependencies {
    api 'org.mariadb.jdbc:mariadb-java-client:3.0.6'
    api 'com.zaxxer:HikariCP:4.0.3'

    api 'org.redisson:redisson:3.20.0'

    api 'org.spongepowered:configurate-core:3.7.2'
    api 'org.spongepowered:configurate-yaml:3.7.2'
}

if (project.hasProperty('generateDocs')) {
    javadoc {
        title = 'BasementLib API (v' + project.ext.fullVersion + ')'
        options.encoding = 'UTF-8'
        options.charSet = 'UTF-8'
        options.links(
                'https://checkerframework.org/api/',
                'https://javadoc.io/static/org.jetbrains/annotations/23.0.0/'
        )
        options.addStringOption('Xdoclint:none', '-quiet')
        if (JavaVersion.current() > JavaVersion.VERSION_1_8) {
            options.links.add('https://docs.oracle.com/en/java/javase/11/docs/api/')
        } else {
            options.links.add('https://docs.oracle.com/javase/8/docs/api/')
        }
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier 'javadoc'
        from javadoc.destinationDir
    }
    task sourcesJar(type: Jar) {
        classifier 'sources'
        from sourceSets.main.allSource
    }
    artifacts {
        archives javadocJar
        archives sourcesJar
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = 'api'
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = 'BasementLib API'
                    description = 'A fundamental library plugin for Minecraft servers.'
                    developers {
                        developer {
                            id = 'ohalee'
                            name = 'ohAlee'
                            email = 'alebartoh@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/ServerBasement/BasementLib.git'
                        developerConnection = 'scm:git:git@github.com:ServerBasement/BasementLib.git'
                        url = 'https://github.com/ServerBasement/BasementLib'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/ServerBasement/BasementLib/issues'
                    }
                }
            }
        }
    }
}